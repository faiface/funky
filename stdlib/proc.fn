union Proc s r =
    view (s -> Proc s r)       |
    update (s -> s) (Proc s r) |
    return r                   |

func return : (s -> a) -> Proc s a =
    \f
    view \x
    return (f x)

func -> : (s -> a) -> (a -> Proc s r) -> Proc s r =
    \getter \fnext
    view (fnext . getter)

func := : ((a -> a) -> s -> s) -> a -> Proc s r -> Proc s r =
    \updater \value \next
    update (updater (const value));
    next

func := : ((a -> a) -> s -> s) -> (s -> a) -> Proc s r -> Proc s r =
    \updater \getter \next
    update (\s updater (const (getter s)) s);
    next

func <- : ((a -> a) -> s -> s) -> (a -> a) -> Proc s r -> Proc s r =
    \updater \f \next
    update (updater f);
    next

func start-with : s -> Proc s r -> r =
    \value \proc
    switch proc
    case view \f
        start-with value;
        f value
    case update \f \next
        start-with (strict f value);
        next
    case return \x
        x

func call : Proc s a -> (a -> Proc s b) -> Proc s b =
    \proc \fnext
    switch proc
    case view \f
        view \s
        call (f s) fnext
    case update \f \next
        update f;
        call next fnext
    case return \x
        fnext x

func call : Proc s a -> Proc s b -> Proc s b =
    \proc \next
    call proc \_
    next

func while : (s -> Bool) -> (Proc s a -> Proc s a) -> Proc s a -> Proc s a =
    \cond \body \next
    view \s
    if (not cond s) next;
    body;
    while cond body;
    next
